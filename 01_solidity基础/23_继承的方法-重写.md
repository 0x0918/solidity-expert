# 第23节：Inheritance、virtual、override

1. 合约之间存在继承关系，使用关键字：is
2. 如果父合约的方法想被子合约继承，则需要使用关键字：virtual
3. 如果子合约想覆盖父合约的方法，则需要使用关键字：override
4. 在子合约中如果想当用父合约的方法，需要使用关键字：super
5. 继承的顺序很重要，遵循最远继承，即后面继承的合约会覆盖前面父合约的方法

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

/* Graph of inheritance
    A
   / \
  B   C
 / \ /
F  D,E

*/

contract A {
    function foo() public pure virtual returns (string memory) {
        return "A";
    }
}

// Contracts inherit other contracts by using the keyword 'is'.
contract B is A {
    // Override A.foo()
    function foo() public pure virtual override returns (string memory) {
        return "B";
    }
}

contract C is A {
    // Override A.foo()
    function foo() public pure virtual override returns (string memory) {
        return "C";
    }
}

// Contracts can inherit from multiple parent contracts.
// When a function is called that is defined multiple times in
// different contracts, parent contracts are searched from
// right to left, and in depth-first manner.

contract D is B, C {
    // D.foo() returns "C"
    // since C is the right most parent contract with function foo()
  	// 构造的顺序是从左到右，即：B，C，D
  	// 最终继承的顺序是：最远继承
    function foo() public pure override(B, C) returns (string memory) {
        return super.foo();
    }
}

contract E is C, B {
    // E.foo() returns "B"
    // since B is the right most parent contract with function foo()
    function foo() public pure override(C, B) returns (string memory) {
        return super.foo();
    }
}

// Inheritance must be ordered from “most base-like” to “most derived”.
// Swapping the order of A and B will throw a compilation error.
// 当继承的父合约中有继承关系时，需要先继承基类，再继承衍生类
contract F is A, B {
    function foo() public pure override(A, B) returns (string memory) {
        return super.foo();
    }
}

//报错，因为继承顺序导致
contract G is B, A {
    function foo() public pure override(A, B) returns (string memory) {
        return super.foo();
    }
}
```